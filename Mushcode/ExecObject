@@ This is the main execscript object.  This interfaces with scripts from the ~/game/scripts directory
@@ If you wish to add more scripts there, feel free to tack on to this
@@
@@ This object _MUST_ be owned and installed by an immortal.
@@
@create ExecObject=10
@desc ExecObject=The following commands exist:%r[space(3)]+diff <text1>/<text2> -- Diff the two texts color highlighting the changes%r[space(3)]+ispell <text> -- perform spelling on the words of <text>%r[space(3)]+dict <word> -- show the dictionary definition of the given word%r[space(3)]+thes <word> -- show the thesaurus matches for the given word%r[space(3)]+math <math> -- evaluate the natural math value and return results%r[space(3)]+pastebinread <hash> -- read the pastebin.com hash specified.%r[space(3)]+pastebinwrite <text> -- write the pastebin text (if allowed)%r[space(3)]+compile <value> -- compile PATCH, RECOMPILE, STATUS, or ROLLBACK.  patch updates code.  Make sure to @readcache after%r[space(3)]+weather <location> -- show current weather at location%r[space(3)]+forcast <location> -- show full weather forcast at location%r%r%rNote: There are more scripts available and you can customize your own.  Please look in your ~/game/scripts directory.
@power/counc ExecObject=execscript
&CMD_DIFF ExecObject=$+diff */*:@pemit %#=[execscript(diff.sh,%0,|,%1)]
&CMD_ISPELL ExecObject=$+ispell*:@pemit %#=[ifelse(!$v(0),+spell: Spell what?,[rjust(- +spell ----,78,-)]%r[execscript(spell.sh,trim(%0))]%r[repeat(-,78)])]
&CMD_DICT ExecObject=$+dict*:@pemit %#=[ifelse(!$v(0),+dict: Define what?,[rjust(- +dict ----,78,-)]%r[execscript(dict.sh,trim(%0))]%r[repeat(-,78)])]
&CMD_MATH ExecObject=$+math*:@pemit %#=[ifelse(match(%0,*=*),[setq(0,before(%0,=))][setq(1,min(255,after(%0,=)))],[setq(0,%0)][setq(1,255)])][ifelse(!$r(0),+math: do what?,[rjust(- +math ----,78,-)]%rMath: %q0 %[precision %q1%]%rResult: [execscript(math.sh,%q0,|,%q1)]%r[repeat(-,78)])]
&CMD_PASTEBINREAD ExecObject=$+pastebinread*:@pemit %#=[ifelse(!$v(0),+pastebinread: Please give the pastebin hash %(example: ES8amsGh%),setq(0,execscript(pastebinread.sh,trim(%0))))][ifelse(!!$r(0),[rjust(- +pastebinread %[hash: [trim(%0)]%] ----,78,-)]%r%q0[repeat(-,78)],+pastebinread: There was no matching hash to read.)]
&CMD_PASTEBINWRITE ExecObject=$+pastebinwrite*:@break [!gt(strlen(%0),40)]=@pemit %#=+pastebinwrite: You need to have at least 40 chars to post on pastebin;@break [lt(secs(),get(%#/_PASTEBIN))]=@pemit %#=+pastebinwrite: Sorry, you need to wait 10 minutes until your next pastebin post.;&_PASTEBIN %#=[add(secs(),600)];@set %#/_pastebin=royalty;@set %#/_pastebin=hidden;@pemit %#=[rjust(- +pastebinpush: %[[strlen(%0)] chars%]----,78,-)]%r[execscript(pastebinwrite.sh,trim(edit(%0,%r,@@@)))][repeat(-,78)]
@set ExecObject/CMD_MATH = no_parse
&CMD_THES ExecObject=$+thes*:@pemit %#=[ifelse(!$v(0),+thes: Thesaurus on what?,[rjust(- +thes ----,78,-)]%r[setq(0,execscript(thes.sh,trim(%0)))][ifelse(!!^r(0),%q0,Nothing found for word.)]%r[repeat(-,78)])]
&CMD_COMPILE ExecObject=$+compile*:@assert [u(CANCOMPILE,%#)]=@pemit %#=+compile: Permission denied;@pemit %#=[execscript(compile.sh,%0)]
&CMD_WEATHER ExecObject=$+weather*:@pemit %#=[ifelse(!$v(0),+weather: Please specify a location,edit(execscript(weather.sh,trim(%0)),?,%<176>))]
&CMD_FORECAST ExecObject=$+forecast*:@pemit %#=[ifelse(!$v(0),+forcast: Please specify a location,edit(execscript(fullweather.sh,trim(%0)),?,%<176>))]
&CMD_LOGSEARCH ExecObject=$+logsearch*:@assert [u(CANLOGSEARCH,%#)]=@pemit %#=+logsearch: Permission denied;@pemit %#=[execscript(logsearch.sh,trim(%0))]
&CANCOMPILE ExecObject=[gte(bittype(%0),6)]
&CANLOGSEARCH ExecObject=[gte(bittype(%0),5)]
&FUN_MATH ExecObject=[ifelse(match(%0,*=*),[setq(0,before(%0,=))][setq(1,min(255,after(%0,=)))],[setq(0,%0)][setq(1,255)])][chomp(execscript(math.sh,objeval(%#,%q0),|,objeval(%#,%q1)))]
&CMD_GITSTATUS ExecObject=$+gitstatus:@assert [u(CANGITSTATUS,%#)]=@pemit %#=+gitstatus: Permission denied;@pemit %#=[chomp(execscript(gitstatus.sh))]
&CANGITSTATUS ExecObject=[gte(bittype(%0),5)]
@startup ExecObject=@dolist lattr(me/fun_*)=@function/priv/pres [after(##,_)]=me/##
@set ExecObject=INHERIT SIDEFX SAFE IND
@tr/quiet ExecObject/startup
