@create Meetme=10
&CMD-MEETME-SUMMON Meetme=$+msummon *:@assert [hasattr(setr(0,pmatch(*%0)),approved)]=@pemit %#=%ch%crMEETME:%cn Sorry, but that person is not approved for this command.;@break [match(get(%#/_meetme),*join*)]=@pemit %#=%ch%crMEETME:%cn Sorry, you can't +msummon while in the process of an +mjoin.;@break [match(get(%#/_meetme),%q0:*)]=@pemit %#=%ch%crMEETME:%cn You are already summoning [cname(%q0)]. Why +msummon a dead horse.;@pemit %q0=%ch%crMEETME:%cn [cname(%#)] would like to summon you. %['%ch%cr+maccept [name(%#)]%cn' to accept or '%ch%cr+mreject [name(%#)]%cn' to reject%]%r[space(8)]Alias: [ifelse(hasattr(%#/alias),get(%#/alias),%(N/A%))][space(10)]Dbref: %#;&_meetme %#=[setunion(get(%#/_meetme),%q0:summon)];@pemit %#=%ch%crMEETME:%cn You have asked [cname(%q0)] if you could summon them.;@wait [v(MEET_TIME)]={@break [setr(1,!member(get(%#/_meetme),%q0:summon))];@pemit %#=%ch%crMEETME:%cn Your offer to +msummon [cname(%q0)] has expired.;@pemit %q0=%ch%crMEETME:%cn %k's request to +msummon you has expired.;&_meetme %#=[setdiff(get(%#/_meetme),%q0:summon)]}
&CMD-MEETME-JOIN-MULTI Meetme=$+mjoin *:@assert [hasattr(setr(0,pmatch(*%0)),approved)]=@pemit %#=%ch%crMEETME:%cn Sorry, but that person is not approved for this command.;@break [or(match(get(%#/_meetme),*summon*),match(get(%#/_meetme),*join*))]=@pemit %#=%ch%crMEETME:%cn Sorry, you can't +mjoin while in the process of an +msummon or another +mjoin.;&_MEETME %#=[setunion(get(%#/_meetme),%q0:join)];@pemit %q0=%ch%crMEETME:%cn [cname(%#)] would like to join you. %['%ch%cr+maccept [name(%#)]%cn' to accept or '%ch%cr+mreject [name(%#)]%cn' to reject%]%r[space(8)]Alias: [ifelse(hasattr(%#/alias),get(%#/alias),%(N/A%))][space(10)]Dbref: %#;@pemit %#=%ch%crMEETME:%cn You have asked [cname(%q0)] if you could join them.;@wait [v(MEET_TIME)]={@break [setr(1,!member(get(%#/_meetme),%q0:join))];@pemit %#=%ch%crMEETME:%cn Your offer to +mjoin [cname(%q0)] has expired.;@pemit %q0=%ch%crMEETME: %cn%k's request to +mjoin you has expired.;&_MEETME %#=[setdiff(get(%#/_meetme),%q0:join)]}
&CMD.MEETME-REJECT-MULTI Meetme=$+mreject *:@break [setq(0,pmatch(%0))][!$setr(1,grab(get(%q0/_meetme),%#:*))]=@pemit %#=%crMEETME:%cn You do not have anything to reject!;@pemit %q0=%ch%crMEETME:%cn [cname(%#)] rejects your offer.;@pemit %#=%ch%crMEETME:%cn [cname(%q0)]'s offer is rejected.;&_MEETME %q0=[setdiff(get(%q0/_meetme),%q1)]
&CREDITS Meetme=Version 1.0.0 Fantasy Moon (A Sailor Moon Mush) -- Coded by Ashen-Shugar
&CMD.MEETME-ACCEPT-MULTI Meetme=$+maccept *:@break [setq(0,pmatch(%0))][!$setr(1,grab(get(%q0/_meetme),%#:*))]=@pemit %#=%ch%crMEETME:%cn You do not have anything to accept!;@pemit %q0=%ch%crMEETME:%cn [ifelse(setr(2,match(%q1,*join*)),Joining,Summoning)] [cname(%#)]...;@pemit %#=%ch%crMEETME:%cn You are [ifelse(%q2,being joined by,being summoned by)] [cname(%q0)].;@tel [ifelse(%q2,%q0,%#)]=[ifelse(%q2,loc(%#),loc(%q0))];&_MEETME %q0=[setdiff(get(%q0/_meetme),%q1)]
&MEET_TIME MeetMe=120
@Aconnect Meetme=@swi %0=0,&_MEETME %#
@set Meetme=INHERIT SAFE
@@
@@ +msummon <player> -- Initiates a summon request to target player.  You can request multiple summons.
@@ +mjoin <player> -- Initiates a join request to the target player.  You can only have one join request active at once.
@@ +mreject <player> -- Reject the player's request (join or summon)
@@ +maccept <player> -- Accept the players request (join or summon)
@@
@@ Default timelimit 120 seconds (set in MEET_TIME attribute)
