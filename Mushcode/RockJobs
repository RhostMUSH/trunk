@@ This is a standard +jobs (otherwise known as +requests) system similar to (but different from) Anomaly Jobs.

@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@                                                                          @@
@@                NO EDITING OF THIS FILE IS NECESSARY                      @@
@@                                                                          @@
@@             BE SURE TO INSTALL MYRDDIN'S BBOARD SYSTEM                   @@
@@                                                                          @@
@@  Just copy-paste this file directly to your game, it will create the     @@
@@  necessary objects for you and modify all referenced dbref's in the      @@
@@  code.                                                                   @@
@@                                                                          @@
@@                    Rockpath's Job System v 1.0.1                         @@
@@                  Quick Fixes to remove Huh? messages                     @@
@@                  Use +jobs/help to see the help file                     @@
@@  Instructions should appear at the bottom when you finish installation.  @@ 
@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

@pemit me=[repeat(=,78)]%r[center(Constructing Rockpath's Job System v 1.0.0,78)]%r[repeat(-,78)]%rPlease wait...%r

@eval [setq(0, [create(Rockpath's Job System)])][setq(1, [create(Rockpath's Job Database)])][set(%q0, dat`database:%q1)][set(%q1, dat`system:%q0)]

@switch/first [setr(0, search(eval=%[match%(%[name%(##%)%], bbpocket%)%]))]=,@pemit %#={[ansi(hg, You do not have a BBoard System in place. Some things may work incorrectly.)])]%r%rContinuing....[set(Rockpath's Job Database, dat`bbpocket:%q0)]}, {@eval [set(Rockpath's Job Database, dat`bbpocket:%q0)]}

@@ Setting self quiet so that you aren't spammed...
@set me=quiet


@lock Rockpath's Job System=me
@Desc Rockpath's Job System=[printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, Jobs System Help)] >)]%r[u([v(dat`database)]/fn`formathelp, {+job/create Category/Topic=Contents - Create a new job. Categories are: [elist([get([v(dat`database)]/list`categories)],,,,,[ansi(y, %0)])]%r+jobs - Look at all the jobs%r+job <#> - Look at the details of a job.%r+jobs/reorder - Reorganize the jobs. Known as 'compress'%r+job/delete <#> - Delete a job. There will be some records created%r+job/approve #=<comment> - Approve a job. If you don't want to add a comment, then leave it blank. But keep the = sign.%r+job/deny <#>=<comment> - Deny a job. If you don't want to add a comment, then leave it blank. But keep the = sign.%r+job/complete <#>=<comment> - Complete a job. If you don't want to add a comment, then leave it blank. But keep the = sign.%r+job/status <#>=<new|underway|hold|done|25|50|75|100> - Set the progress of the job%r+job/comment <#>=<comment> - Comment on a job. This is simply appended to the job description.%r+job/urgency <#>=<1-4|red|yellow|green> - Set the urgency of a job.%r+job/rename <#>=<New Name> - Rename a job. It does /not/ change the Number of a job.%r+jobs/search <Search Term> - Search for a job with a particular name. Or search for a job with a job description containing <search term>%r+job/claim <#> - Claim a job for yourself.%r+job/assign <#>=<Staff Member> - Assign a job to another person. This job will alert other Staff.%r+jobs/mine - List jobs assigned to you.%r+jobs/cat <#>=<Category> - Change a job's category.%r+jobs/list <category> - List jobs in a category%r+jobs/tier <Job #>=<Tier #> - Set a job to a specific tier.})]%r[printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, End Help)] >)]

&CMD`ADD Rockpath's Job System=$+job/create */*=*:[switch(0, [gte([bittype(%#)], 5)], [pemit(%#, [prompt(game)] You don't have permission to do that!)], [match([get([v(dat`database)]/list`categories)], %0)], [pemit(%#, [prompt(Jobs)] Invalid Category. Please choose from [elist([get([v(dat`database)]/list`categories)], or)].)], [setq(0, [create(Job [get([v(dat`database)]/count)])])][set([v(dat`database)], count:[inc([get([v(dat`database)]/count)])])][set(%q0, requester:%#)][tel(%q0, [v(dat`database)])][set(%q0, JName:%1)][set(%q0, Tier:1)][set(%q0, Category:[ucstr(%0)])][set(%q0, describe:%2)][set(%q0, status:NEW)][set(%q0, Urgency:1)][set(%q0, started:[secs()])][pemit(%#, [prompt(Jobs)] You have created [name(%q0)] under the %0 topic!)][pemit(search(eplayer=gt(bittype(##), 4)), [prompt(jobs)] [name(%#)] has added a new Job under the %0 topic.)])]

&CMD`VIEW-ALL Rockpath's Job System=$+jobs:@switch/first 1=[gte([bittype(%#)], 5)], {[pemit(%#, [printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, [mudname() Jobs])] >)]%r[ansi(c, [printf($7^s $9-s $25-s $8-s $15-s $8^s, Job #, Category, Job Title, Started, Handler, Status)])]%r[repeat(-, 78)])][if([t([lcon([v(dat`database)]/object)])], [iter([revwords([lcon([v(dat`database)]/object)])], [pemit(%#, [ansi([switch([get(##/urgency)], 1, hg, 2, hy, 3, hr, n)], [printf($7^s $9-s $25-s $8-s $15-s $8s, [name(##)], [get(##/category)], [get(##/JName)], [if([hasattr(##/started)], [timefmt($02M/$02D/$y, [get(##/started)])], --------)], [if([hasattr(##, handler)], [name([get(##/handler)])], -----)], [get(##/status)])])])])])][pemit(%#,[printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, End Jobs)] >)])]}, [gte([bittype(%#)], 3)], {[pemit(%#, [printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, [mudname() Jobs])] >)]%r[ansi(c, [printf($7^s $9-s $25-s $8-s $15-s $8^s, Job #, Category, Job Title, Started, Handler, Status)])]%r[repeat(-, 78)])][if([t([lcon([v(dat`database)]/object)])], [iter([revwords([lcon([v(dat`database)]/object)])], [if([gt([get(##/tier)], 1)], [pemit(%#, [ansi([switch([get(##/urgency)], 1, hg, 2, hy, 3, hr, n)], [printf($7^s $9-s $25-s $8-s $15-s $8s, [name(##)], [get(##/category)], [get(##/JName)], [if([hasattr(##/started)], [timefmt($02M/$02D/$y, [get(##/started)])], --------)], [if([hasattr(##, handler)], [name([get(##/handler)])], -----)], [get(##/status)])])])])])])][pemit(%#,[printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, End Jobs)] >)])]}, [pemit(%#, [prompt(game)] You don't have permission to do that >:|)]

&CMD`VIEW-JOB Rockpath's Job System=$+job *:@switch/first 0=[gte([bittype(%#)], 3)], [pemit(%#, [prompt(game)] You don't have permission to do that! :<)], [isnum(%0)], [pemit(%#, [prompt(game)] You must specify a number.)], [and([gt(%0, 0)],[gte([get([v(dat`database)]/count)], %0)])], [pemit(%#, [prompt(jobs)] Invalid job #! Please choose a number listed in +jobs.)], [t([setr(0, [u([v(dat`database)]/fn`get-job, %0)])])], [pemit(%#, [prompt(jobs)] Invalid job!)], [if([gte([bittype(%#)], 5)], [u([v(dat`database)]/fn`showjob)], [if([gte([get(%q0/tier)], 2)], [u([v(dat`database)]/fn`showjob)], [pemit(%#, [prompt(jobs)] You cannot view that job.)])])]

&CMD`REORDER Rockpath's Job System=$+jobs/reorder:@skip/ifelse [strmatch(%#, [owner(me)])]={@cemit staff={[prompt(jobs)] The jobs were reordered.};@eval [set([v(dat`database)], count:1)][iter([revwords([lcon([v(dat`database)]/object)])], [name(##, Job [get([v(dat`database)]/count)])][set([v(dat`database)], count:[inc([get([v(dat`database)]/count)])])])][pemit(%#, [prompt(jobs)] You have reordered all the jobs!)]}, @pemit %#=[prompt(game)] You can't do that :|

&CMD`DELETE Rockpath's Job System=$+job/delete *:@switch/first 0=[gte([bittype(%#)], 3)], [pemit(%#, [prompt(game)] You don't have permission to do that! :<)], [isnum(%0)], [pemit(%#, [prompt(game)] You must specify a number.)], [and([gt(%0, 0)],[gte([get([v(dat`database)]/count)], %0)])], [pemit(%#, [prompt(jobs)] Invalid job #! Please choose a number listed in +jobs.)], [t([setr(0, [u([v(dat`database)]/fn`get-job, %0)])])], [pemit(%#, [prompt(jobs)] Invalid job!)], [if([gte([bittype(%#)], 5)], [u([v(dat`database)]/fn`del-job)], [if([gte([get(%q0/tier)], 2)], [u([v(dat`database)]/fn`del-job)], [pemit(%#, [prompt(jobs)] You cannot delete that job.)])])]

&CMD`APPROVE Rockpath's Job System=$+job/approve *=*:@switch/first 0=[gte([bittype(%#)], 3)], [pemit(%#, [prompt(game)] You don't have permission to do that! :<)], [isnum(%0)], [pemit(%#, [prompt(game)] You must specify a number.)], [and([gt(%0, 0)],[gte([get([v(dat`database)]/count)], %0)])], [pemit(%#, [prompt(jobs)] Invalid job #! Please choose a number listed in +jobs.)], [t([setr(0, [u([v(dat`database)]/fn`get-job, %0)])])], [pemit(%#, [prompt(jobs)] Invalid job!)], [if([gte([bittype(%#)], 5)], [u([v(dat`database)]/fn`approve-job, %1)], [if([gte([get(%q0/tier)], 2)], [u([v(dat`database)]/fn`approve-job, %1)], [pemit(%#, [prompt(jobs)] You cannot approve that job.)])])]

&CMD`DENY Rockpath's Job System=$+job/deny *=*:@switch/first 0=[gte([bittype(%#)], 3)], [pemit(%#, [prompt(game)] You don't have permission to do that! :<)], [isnum(%0)], [pemit(%#, [prompt(game)] You must specify a number.)], [and([gt(%0, 0)],[gte([get([v(dat`database)]/count)], %0)])], [pemit(%#, [prompt(jobs)] Invalid job #! Please choose a number listed in +jobs.)], [t([setr(0, [u([v(dat`database)]/fn`get-job, %0)])])], [pemit(%#, [prompt(jobs)] Invalid job!)], [if([gte([bittype(%#)], 5)], [u([v(dat`database)]/fn`deny-job, %1)], [if([gte([get(%q0/tier)], 2)], [u([v(dat`database)]/fn`deny-job, %1)], [pemit(%#, [prompt(jobs)] You cannot deny that job.)])])]

&CMD`COMPLETE Rockpath's Job System=$+job/complete *=*:@switch/first 0=[gte([bittype(%#)], 3)], [pemit(%#, [prompt(game)] You don't have permission to do that! :<)], [isnum(%0)], [pemit(%#, [prompt(game)] You must specify a number.)], [and([gt(%0, 0)],[gte([get([v(dat`database)]/count)], %0)])], [pemit(%#, [prompt(jobs)] Invalid job #! Please choose a number listed in +jobs.)], [t([setr(0, [u([v(dat`database)]/fn`get-job, %0)])])], [pemit(%#, [prompt(jobs)] Invalid job!)], [if([gte([bittype(%#)], 5)], [u([v(dat`database)]/fn`complete-job, %1)], [if([gte([get(%q0/tier)], 2)], [u([v(dat`database)]/fn`complete-job, %1)], [pemit(%#, [prompt(jobs)] You cannot complete that job.)])])]

&CMD`COMMENT Rockpath's Job System=$+job/comment *=*:@skip/ifelse [orflags(%#, Wai)]={[setq(0, [u([v(dat`database)]/fn`get-job, %0)])][switch(0, [isint(%0)], [pemit(%#, [prompt(jobs)] Job must be a number!)], [t(%q0)], [pemit(%#, [prompt(jobs)] No such job!)], [pemit(%#, [prompt(Jobs)] You have commented on [name(%q0)].)][set(%q0, desc:[strcat([get(%q0/desc)], %r%r[name(%#)] %[[timefmt($M/$D/$Y $H:$02T$p, [secs()])]%]: %1)])][mailsend([get(%q0/requester)] [if([t([get(%q0/handler)])], [get(%q0/handler)])], [name(%q0)] %([get(%q0/jname)]%), [name(%#)] %[[timefmt($M/$D/$Y $H:$02T$p, [secs()])]%]: %1)])]}, [pemit(%#, [prompt(game)] You don't have permission to do that.)]

&CMD`STATUS Rockpath's Job System=$+job/status *=*:@switch/first 0=[gte([bittype(%#)], 3)], [pemit(%#, [prompt(game)] You don't have permission to do that! :<)], [isnum(%0)], [pemit(%#, [prompt(game)] You must specify a number.)], [and([gt(%0, 0)],[gte([get([v(dat`database)]/count)], %0)])], [pemit(%#, [prompt(jobs)] Invalid job #! Please choose a number listed in +jobs.)], [t([setr(0, [u([v(dat`database)]/fn`get-job, %0)])])], [pemit(%#, [prompt(jobs)] Invalid job!)], [if([gte([bittype(%#)], 5)], [u([v(dat`database)]/fn`status,%q0,%1)], [if([gte([get(%q0/tier)], 2)], [u([v(dat`database)]/fn`status,%q0,%1)], [pemit(%#, [prompt(jobs)] You cannot change that job's status.)])])]

&CMD`URGENT Rockpath's Job System=$+job/urgency *=*:@switch/first 0=[gte([bittype(%#)], 3)], [pemit(%#, [prompt(game)] You don't have permission to do that! :<)], [isnum(%0)], [pemit(%#, [prompt(game)] You must specify a number.)], [and([gt(%0, 0)],[gte([get([v(dat`database)]/count)], %0)])], [pemit(%#, [prompt(jobs)] Invalid job #! Please choose a number listed in +jobs.)], [t([setr(0, [u([v(dat`database)]/fn`get-job, %0)])])], [pemit(%#, [prompt(jobs)] Invalid job!)], [if([gte([bittype(%#)], 5)], [u([v(dat`database)]/fn`urgent,%q0,%1)], [if([gte([get(%q0/tier)], 2)], [u([v(dat`database)]/fn`urgent,%q0,%1)], [pemit(%#, [prompt(jobs)] You cannot change that job's urgency.)])])]

&CMD`RENAME Rockpath's Job System=$+job/rename *=*:@skip/ifelse [orflags(%#, Wai)]={[setq(0, [u([v(dat`database)]/fn`get-job, %0)])][switch(0, [isint(%0)], [pemit(%#, [prompt(jobs)] Job must be a number!)], [t(%q0)], [pemit(%#, [prompt(jobs)] No such job!)], [set(%q0, jname:%1)][pemit(%#, [prompt(Jobs)] You have set [name(%q0)]'s Job Name as '%1'.)])]}, [pemit(%#, [prompt(game)] You don't have permission to do that.)]

&CMD`SEARCH Rockpath's Job System=$+jobs/search *:@skip/ifelse [orflags(%#, Wai)]={[pemit(%#, [printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, [mudname()] Jobs: Name Search Results)] >)]%r[ansi(c, [printf($7^s $9-s $25-s $8-s $15-s $8^s, Job #, Category, Job Title, Started, Handler, Status)])]%r[repeat(-, 78)])][iter([revwords([lcon([v(dat`database)]/object)])], [if([t([grep(##, jname, %0)])], [pemit(%#, [ansi([switch([get(##/urgency)], 1, hg, 2, hy, 3, hr, n)], [printf($7^s $9-s $25-s $8-s $15-s $8s, [name(##)], [get(##/category)], [get(##/JName)], [if([hasattr(##/started)], [timefmt($02M/$02D/$y, [get(##/started)])], --------)], [if([hasattr(##, handler)], [name([get(##/handler)])], -----)], [get(##/status)])])])])])][pemit(%#, [printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, NMR 3.0 Jobs: Content Search Results)] >)]%r[ansi(c, [printf($7^s $9-s $25-s $8-s $15-s $8^s, Job #, Category, Job Title, Started, Handler, Status)])]%r[repeat(-, 78)])][iter([revwords([lcon([v(dat`database)]/object)])], [if([t([grep(##, desc, %0)])], [pemit(%#, [ansi([switch([get(##/urgency)], 1, hg, 2, hy, 3, hr, n)], [printf($7^s $9-s $25-s $8-s $15-s $8s, [name(##)], [get(##/category)], [get(##/JName)], [if([hasattr(##/started)], [timefmt($02M/$02D/$y, [get(##/started)])], --------)], [if([hasattr(##, handler)], [name([get(##/handler)])], -----)], [get(##/status)])])])])])][pemit(%#, [printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, End Search)] >)])]}, [pemit(%#, [prompt(game)] You don't have permission to do that D:)]

&CMD`CLAIM Rockpath's Job System=$+job/claim *:@skip/ifelse [orflags(%#, Wai)]={[setq(0, [u([v(dat`database)]/fn`get-job, %0)])][switch(0, [isint(%0)], [pemit(%#, [prompt(jobs)] Job must be a number!)], [t(%q0)], [pemit(%#, [prompt(jobs)] No such job!)], [pemit(%#, [prompt(Jobs)] You have claimed [name(%q0)].)][set(%q0, handler:%#)][pemit(search(eplayer=%[orflags(##, Wia)%]), [prompt(jobs)] %n has claimed [name(%q0)].)])]}, [pemit(%#, [prompt(game)] You don't have permission to do that.)]

&CMD`ASSIGN Rockpath's Job System=$+job/assign *=*:@skip/ifelse [orflags(%#, Wai)]={[setq(0, [u([v(dat`database)]/fn`get-job, %0)])][switch(0, [isint(%0)], [pemit(%#, [prompt(jobs)] Job must be a number!)], [t(%1)], [set(%q0, handler:)][pemit(%#, [prompt(jobs)] You have removed a handler from [name(%q0)].)], [t(%q0)], [pemit(%#, [prompt(jobs)] No such job!)], [t([setr(1, [pmatch(%1)])])], [pemit(%#, [prompt(jobs)] You must assign the job to a player. [ansi(y, %1)] is not a player.)], [orflags(%q1, Wai)], [pemit(%#, [prompt(jobs)] You must assign the job to a staff member!)], [pemit(%#, [prompt(Jobs)] You have assigned [name(%q0)] to [name(%q1)].)][pemit(search(eplayer=gte(bittype(##), 5)), [prompt(jobs)] %n has assigned [name(%q0)] to [name(%q1)].)][set(%q0, handler:%q1)][pemit(%q1, [prompt(jobs)] You have been assigned [name(%q0)].)])]}, [pemit(%#, [prompt(game)] You don't have permission to do that.)]

&CMD`MYJOBS Rockpath's Job System=$+jobs/mine:@skip/ifelse [orflags(%#, Wai)]={[pemit(%#, [printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, [mudname()] Jobs)] >)]%r[ansi(c, [printf($7^s $9-s $25-s $8-s $15-s $8^s, Job #, Category, Job Title, Started, Handler, Status)])]%r[repeat(-, 78)])][iter([revwords([lcon([v(dat`database)]/object)])], [ifelse([and([hasattr(##, handler)], [strmatch(%#, [get(##/handler)])])], [pemit(%#, [ansi([switch([get(##/urgency)], 1, hg, 2, hy, 3, hr, n)], [printf($7^s $9-s $25-s $8-s $15-s $8s, [name(##)], [get(##/category)], [get(##/JName)], [if([hasattr(##/started)], [timefmt($02M/$02D/$y, [get(##/started)])], --------)], [if([hasattr(##, handler)], [name([get(##/handler)])], -----)], [get(##/status)])])])])])][pemit(%#,[printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, End Jobs)] >)])]}, [pemit(%#, [prompt(game)] You don't have permission to do that >:|)]

&CMD`CATEGORIZE Rockpath's Job System=$+job/cat *=*:@skip/ifelse [orflags(%#, Wai)]={[setq(0, [u([v(dat`database)]/fn`get-job, %0)])][switch(0, [isint(%0)], [pemit(%#, [prompt(jobs)] Job must be a number!)], [t(%q0)], [pemit(%#, [prompt(jobs)] No such job!)], [match([get([v(dat`database)]/list`categories)], %1)], [pemit(%#, [prompt(jobs)] You need to pick a valid category! [get([v(dat`database)]/list`categories)])], [set(%q0, category:[ucstr(%1)])][pemit(%#, [prompt(Jobs)] You have set [name(%q0)]'s Category to '%1'.)])]}, [pemit(%#, [prompt(game)] You don't have permission to do that >:|)]

&CMD`CATLIST Rockpath's Job System=$+job/list *:@skip/ifelse [orflags(%#, Wai)]={[if([t([match([get([v(dat`database)]/list`categories)], %0)])], [pemit(%#, [printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, [mudname()] Jobs)] >)]%r[ansi(c, [printf($7^s $9-s $25-s $8-s $15-s $8^s, Job #, Category, Job Title, Started, Handler, Status)])]%r[repeat(-, 78)])][iter([revwords([lcon([v(dat`database)]/object)])], [if([match(%0, [get(##/category)])], [pemit(%#, [ansi([switch([get(##/urgency)], 1, hg, 2, hy, 3, hr, n)], [printf($7^s $9-s $25-s $8-s $15-s $8s, [name(##)], [get(##/category)], [get(##/JName)], [if([hasattr(##/started)], [timefmt($02M/$02D/$y, [get(##/started)])], --------)], [if([hasattr(##, handler)], [name([get(##/handler)])], -----)], [get(##/status)])])])])])][pemit(%#,[printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, End Jobs)] >)])], [pemit(%#, [prompt(jobs)] You must choose a valid Category from: [get([v(dat`database)]/list`categories)])])]}, [pemit(%#, [prompt(game)] You don't have permission to do that >:|)]

&CMD`HELP Rockpath's Job System=$+jobs/help:@skip/ifelse [orflags(%#, BWagi)]={[pemit(%#, [u(desc)])]}, [pemit(%#, [u([v(dat`database)]/req-help)])]

&REQ-HELP Rockpath's Job System=[printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, Request System Help)] >)]%r[formathelp({+request - Start going through a +request.%r+request/create <Category>/<Topic>=<Contents> - Create a new request. The categories are: [elist([get([v(dat`database)]/list`categories)],or,,,,[ansi(y, %0)])]%r+requests - Look at all the requests you've made.%r+request <#> - Look at the details of a request.%r+request/comment <#>=<comment> - Comment on a request. This is simply appended to the request description.})]%r[printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, End Help)] >)]

&CMD`REQ-ADD Rockpath's Job System=$+request/create */*=*:[switch(0, [match([get([v(dat`database)]/list`categories)], %0)], [pemit(%#, [prompt(Jobs)] Invalid Category. Please choose from [elist([get([v(dat`database)]/list`categories)], or)].)], [setq(0, [create(Job [get([v(dat`database)]/count)])])][set([v(dat`database)], count:[inc([get([v(dat`database)]/count)])])][set(%q0, requester:%#)][tel(%q0, [v(dat`database)])][set(%q0, JName:%1)][set(%q0, Category:[ucstr(%0)])][set(%q0, describe:%2)][set(%q0, status:NEW)][set(%q0, Urgency:1)][set(%q0, started:[secs()])][pemit(%#, [prompt(Jobs)] You have created Request [dec([get([v(dat`database)]/count)])] under the %0 topic!)][pemit(search(eplayer=gt(bittype(##), 4)), [prompt(jobs)] [name(%#)] has added a new Job under the %0 topic.)])]

&CMD`VIEW-ALL-REQ Rockpath's Job System=$+requests:[pemit(%#, [printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, [mudname() Jobs])] >)]%r[ansi(c, [printf($7^s $8-s $25-s $8-s $15-s $8^s, Req #, Category, Request Title, Started, Handler, Status)])]%r[repeat(-, 78)])][iter([revwords([lcon([v(dat`database)]/object)])], [if([strmatch(%#, [get(##/requester)])], [pemit(%#, [printf($7^s $8-s $25-s $8-s $15-s $8^s, [edit([name(##)], Job, Req)], [get(##/category)], [get(##/JName)], [if([hasattr(##/started)], [timefmt($02M/$02D/$y, [get(##/started)])], --------)], [if([hasattr(##, handler)], [name([get(##/handler)])], -----)], [ansi([switch([get(##/urgency)], 1, hg, 2, hy, 3, hr, n)], [get(##/status)])])])])])][pemit(%#,[printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, End Jobs)] >)])]

&CMD`VIEW-REQ Rockpath's Job System=$+request *:@skip/ifelse [strmatch(%#, [get([u([v(dat`database)]/fn`get-job, %0)]/requester)])]={[setq(0, [u([v(dat`database)]/fn`get-job, %0)])][switch(0, [isint(%0)], [pemit(%#, [prompt(jobs)] You must specify a number to view!)], [and([gte(%0, 0)],[gte([get([v(dat`database)]/count)], %0)])], [pemit(%#, [prompt(jobs)] Invalid request #! Please choose a number listed in +requests.)], [t(%q0)], [pemit(%#, [prompt(Jobs)] Invalid request.)], [u([v(dat`database)]/fn`showjob)])]}, [pemit(%#, [prompt(jobs)] That request isn't yours!)]

&CMD`REQ-COMMENT Rockpath's Job System=$+request/comment *=*:@skip/ifelse [strmatch(%#, [get([u([v(dat`database)]/fn`get-job, %0)]/requester)])]={[setq(0, [u([v(dat`database)]/fn`get-job, %0)])][switch(0, [isint(%0)], [pemit(%#, [prompt(jobs)] Request must be a number!)], [t(%q0)], [pemit(%#, [prompt(jobs)] No such request!)], [pemit(%#, [prompt(Jobs)] You have commented on [edit([name(%q0)], Job, Request)].)][set(%q0, desc:[strcat([get(%q0/desc)], %r%r[name(%#)] %[[timefmt($M/$D/$Y $H:$02T$p, [secs()])]%]: %1)])])]}, [pemit(%#, [prompt(game)] You don't have permission to do that.)]

&CMD`REQ-HELP Rockpath's Job System=$+request/help:@eval [pemit(%#, [u(me/req-help)])]

&CMD`VIEW-ALL2 Rockpath's Job System=$+job:@include/command me/cmd`view-all

&CMD`TIER Rockpath's Job System=$+job/tier *=*:@skip/ifelse [orflags(%#, Wai)]={[setq(0, [u([v(dat`database)]/fn`get-job, %0)])][switch(0, [isint(%0)], [pemit(%#, [prompt(jobs)] Job must be a number!)], [t(%q0)], [pemit(%#, [prompt(jobs)] No such job!)], [pemit(%#, [prompt(Jobs)] You have changed the tier of [name(%q0)] to %1.)][set(%q0, tier:%1)])]}, [pemit(%#, [prompt(game)] You don't have permission to do that.)]

@set Rockpath's Job System=INHERIT SIDEFX !HALT

@toggle Rockpath's Job System=PENN_MAIL


&FN`FORMATHELP Rockpath's Job Database=[regeditalli(%0,(\\\\+((\\\\w)+(/(\\\\w)+)?)),[ansi(hg,$1)],(<(.*?)>),[ansi(hr,$1)],(=),[ansi(hc,$1)],(\\\\((.*)\\\\)),[ansi(+white,%(, +white, $2, +white, %))],('(.*?)'),[ansi(hm,'$2')], %{,,%},)]

&count Rockpath's Job Database=1

&FN`GET-JOB Rockpath's Job Database=[num(Job %0)]

&FN`SHOWJOB Rockpath's Job Database=[pemit(%#, [printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, [name(%q0)])] >)])][pemit(%#, [printf($50-s $25-s, [ansi(hc, Job Title)]: [get(%q0/jname)], [ansi(hc, Requester)]: [name([get(%q0/requester)])])]%r[printf($25-s $25-s $25-s, [ansi(hc, Category)]: [get(%q0/category)],, [ansi(hc, Status)]: [switch([get(%q0/urgency)], 1, Green, 2, Yellow, 3, Red)] %([get(%q0/status)]%))]%r[printf($50-s $25-s, [ansi(hc, Created)]: [createtime(%q0)], [ansi(hc, Handler)]: [if([hasattr(%q0, handler)], [name([get(%q0/handler)])], -----)])])][pemit(%#, [repeat([ansi(hb, -)], 78)])][pemit(%#, [get(%q0/describe)])][pemit(%#, [printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, End Job)] >)])]

&FN`MAIL Rockpath's Job Database=@trigger tr`mail=%0

&FN`POST Rockpath's Job Database=+bbpost [v(dat`bbpocket)]/Deleted: [name(%q0)]=[u(%q0/describe)]

@Startup Rockpath's Job Database=@trigger [v(dat`database)]/tr`urgency

&TR`URGENCY Rockpath's Job Database=@wait 1={th [setq(z, [secs()])][iter([lcon(me/object)], [setq(y, [sub(%qz, [get(##/started)])])][switch(1, [!hasattr(##/started)], [set(##, urgency:4)], [eq([get(##/urgency)], 3)],, [eq([get(##/urgency)], 4)], , [lte(604800, %qy)], [set(##, urgency:3)], [lte(432000, %qy)], [set(##, urgency:2)])])]Test}

&REQ-HELP Rockpath's Job Database=[printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, Request System Help)] >)]%r[formathelp({+request/create Category/Topic=Contents - Create a new request.%r+requests - Look at all the requests you've made.%r+request <#> - Look at the details of a request.%r+request/comment <#>=<comment> - Comment on a request. This is simply appended to the request description.})]%r[printf($^78:[ansi(hb, -=)]:s, < [ansi(hw, End Help)] >)]

&LIST`CATEGORIES Rockpath's Job Database=ADMIN APP BUILD CG CODE FORUM BUG PITCH TYPO PLOT THEME QUERY NONE

&TR_POST_NOTIFY Rockpath's Job Database=[pemit(iter(setunion(lwho(),),if(u(%1/canread,##),##)), (New BB message ([member(u([v(dat`bbpocket)]/groups),%1)]/%2) posted to '[name(%1)]' by [ifelse(hasattr(%1,anonymous),get(%1/anonymous),name(%0))]: %3))]

&FN`BBPOST Rockpath's Job Database=[switch([member([u([v(dat`bbpocket)]/valid_groups, %#, write)], [setr(9, [u([v(dat`bbpocket)]/get_group, %0)])])], [set(%q9, mess_lst:[cat([get(%q9/mess_lst)],[setr(8,[get([v(dat`bbpocket)]/nxt_mess)])])])][set([v(dat`bbpocket)], nxt_mess:[ulocal([v(dat`bbpocket)]/fn_inc_next_mess, %q8)])][set([v(dat`bbpocket)], master_lst:[cat(get([v(dat`bbpocket)]/master_lst),%q8)])][set(%q9, hdr_%q8:[mid(%1, 0, 34)]|[u([v(dat`bbpocket)]/bbtime)]|[edefault(%q9/anonymous,mid(name(%#),0,24))]|[owner(%#)]|[ifelse(get(%q9/config_timeout),add(get(%q9/config_timeout),secs()),0)])][set(%q9, last_mod:[u([v(dat`bbpocket)]/bbtime)])][set(%q9, bdy_%q8:[%2][if([and([hasattr(%#, bb_sig)], [not([hasattr(%q9, anonymous)])])], %r[ulocal(%#/bb_sig)])])][set(%#, bb_read:[setunion(get(%#/bb_read),%q8)])][u(tr_post_notify, %#, %q9, [member(get(%q9/mess_lst),%q8)],[mid(%1, 0, 34)])])]

&FN`DEL-JOB Rockpath's Job Database=[mailsend([get(%q0/requester)], Del: [name(%q0)], [cname(%#)] has deleted your request. A copy has been attached.%r%r[get(%q0/describe)])][u([v(dat`database)]/fn`bbpost, [v(dat`bb-group)], Delete: [get(%q0/jname)], [name(%q0)] from [name([get(%q0/requester)])] has been deleted by [cname(%#)]:%r%r[get(%q0/desc)])][destroy(%q0)][pemit(%#, [prompt(jobs)] You have deleted [name(%q0)].)]

&FN`APPROVE-JOB Rockpath's Job Database=[mailsend([get(%q0/requester)], App: [name(%q0)],[cname(%#)] has approved your request. A copy has been attached.%r%r[get(%q0/describe)]%r[repeat(-, 78)]%rComments from [cname(%#)]: %0)][u([v(dat`database)]/fn`bbpost, [v(dat`bb-group)], A: [get(%q0/jname)], [name(%q0)] from [name([get(%q0/requester)])] has been approved by [cname(%#)]:%r%r[get(%q0/desc)]%r[repeat(-, 78)]%rComments from [cname(%#)]: %0)][destroy(%q0)][pemit(%#, [prompt(jobs)] You have approved [name(%q0)].)]

&FN`DENY-JOB Rockpath's Job Database=[mailsend([get(%q0/requester)], Den: [name(%q0)],[cname(%#)] has denied your request. A copy has been attached.%r%r[get(%q0/describe)]%r[repeat(-, 78)]%rComments from [cname(%#)]: %0)][u([v(dat`database)]/fn`bbpost, [v(dat`bb-group)], D: [get(%q0/jname)], [name(%q0)] from [name([get(%q0/requester)])] has been denied by [cname(%#)]:%r%r[get(%q0/desc)]%r[repeat(-, 78)]%rComments from [cname(%#)]: %0)][destroy(%q0)][pemit(%#, [prompt(jobs)] You have denied [name(%q0)].)]

&FN`COMPLETE-JOB Rockpath's Job Database=[mailsend([get(%q0/requester)], Com: [name(%q0)],[cname(%#)] has completed your request. A copy has been attached.%r%r[get(%q0/describe)]%r[repeat(-, 78)]%rComments from [cname(%#)]: %0)][u([v(dat`database)]/fn`bbpost, [v(dat`bb-group)], C: [get(%q0/jname)], [name(%q0)] from [name([get(%q0/requester)])] has been completed by [cname(%#)]:%r%r[get(%q0/desc)]%r[repeat(-, 78)]%rComments from [cname(%#)]: %0)][destroy(%q0)][pemit(%#, [prompt(jobs)] You have completed [name(%q0)])]

&FN`STATUS Rockpath's Job Database=[set(%0, status:[setr(3, [switch(%1, new, NEW, underway, UNWY, done, DONE, complete, DONE, 50, 1/2 Done, 25, 1/4 Done, 75, 3/4 Done, 100, DONE, on hold, HOLD, hold, HOLD, ---)])])][pemit(%#, [prompt(Jobs)] You have set [name(%q0)]'s status as '%q3'.)]

&FN`URGENT Rockpath's Job Database=[set(%0, urgency:[setr(3, [switch(%1, 1, 1, 2, 2, 3, 3, green, 1, yellow, 2, red, 3, 4)])])][pemit(%#, [prompt(Jobs)] You have set [name(%0)]'s urgency as 'Level %q3'.)])]

&CREDIT Rockpath's Job Database=Inspiration taken from Anomaly Jobs. This setup-file was based off of Myrddin's BBoard Setup File, and there is some code within here so that things are compatible between the BBoard System she created and the Jobs System that I (Rockpath) created. The code above is all my own doing.

&Last_UPdate Rockpath's Job Database=October 23, 2016

@set Rockpath's Job Database=INHERIT SIDEFX

@set me=!quiet

@wait 1=@pemit me=%Jobs System construction complete. It is suggested that you create a Jobs_System character with Immortal permissions so that mails don't look like they come from a single staffer.%r%r[ansi(hg, Please create a new BBGroup and set the group with &dat`bb-group Rockpath's Job Database=<DBref of bb-group>.)]%r[repeat(=,78)]
