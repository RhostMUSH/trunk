@@ The roster object has help built in when you look at it.
@@ Roster/Status/Info Object
@create Roster Object <RO>=10
&FN_HEADER Roster Object <RO>=[setq(z,v(color))][printf($68:[ansi(h%qz,-)]:s[ansi(h%qz,----------)],%b %0 %b)]
&FN_FOOTER Roster Object <RO>=[setq(z,v(color))][ansi(h%qz,repeat(-,78))]
&FN_TITLE Roster Object <RO>=[get(*%0/titlecaption)]
&VERSION Roster Object <RO>=1.0.1
&COLOR Roster Object <RO>=r
&COLOR_NA Roster Object <RO>=m
&COLOR_COLON Roster Object <RO>=b
@startup Roster Object <RO>=@lfunction header=me/fn_header;@lfunction footer=me/fn_footer;@lfunction/priv title=me/fn_title
&LIST_ORDER Roster Object <RO>=FULLNAME|2|1|40||-| AGE|2|2|10||-| POSITION|4|2|32||-| INFO|3|1|78||-| SEX|2|3|10|Moany|-| @GUILD|1|1|40||-| @NAME|1|2|30||-|
&CMD_INFO Roster Object <RO>=$+info *:@pemit %#=[setq(z,v(color))][setq(y,v(color_na))][setq(x,v(color_colon))][u(F_INFO[isdbref(setr(0,pmatch(%0)))],%q0)]
&F_INFO0 Roster Object <RO>=+info: Sorry, can not get any info on '[ifelse(hastype(%0,player),name(%0),<unrecognized player>)]'
&F_INFO1 Roster Object <RO>=[list(lnum(1,lmax(iter(v(list_order),extract(itext(0),2,1,|)))),u(f_format,##,%0),,header(+info),%#)][footer()]
@Desc Roster Object <RO>=[header(Roster Help)]%rVersion: [v(version)]%rThe following attributes are used for ordering and configuration:%rFN_HEADER -- header function (if none defined)%rFN_FOOTER -- footer function (if none defined)%rFN_TITLE -- Title function (if none defined)%rCOLOR -- default color for borders/labels%rCOLOR_NA -- default color for No-Matching Attribute Found%rCOLOR_COLON -- color of colons and roster headers%rLIST_ORDER -- This handles the attributes that are parsed, the order they are in, and how much padding it handles. Attributes starting with a '@' specify to use a function instead passing in the player name as the only argument.%r%rSyntax: <ATTRIBUTE or FUNCTION>|<ROW>|<COLUMN PRIORITY>|<PADDING>|<OPTIONAL FIELD NAME>|<JUSTIFICATION>%rExamples:%r[space(5)]FINGER`TITLE|1|2|20|- -- Fetch 'FINGER`TITLE' (shortening display as title)%r[space(20)] Formatting: row 1, column 2, padd 20, left justify%r[space(5)]@name|2|1|30|| -- Execute name() function on target%r[space(20)] Formatting: row 2, column 1, padd 30, right justify (default)%r[space(5)]SEX|3|1|30|SnooSnoo|^ -- Fetch 'Sex', call it 'SnooSnoo' on header.%r[space(20)] Formatting: row 3, column 1, padd 30, header override to SnooSnoo, center justify%r[space(5)]@ADD:1:2:3:4:5|6|1|20|Math|^ -- Execute add() with args 1,2,3,4,5 (returning 15).%r[space(20)] Formatting: row 6, column 1, padd 20, header override to Math, center justify.%r%rNote: Using 'NH' for the optional name argument tells it to surpress the name altogther.%r[space(6)]Use the printf() chars (-^_) for right, center, justified justification.%r[space(6)]Substitutions of %%# and %%l allowed as arguments.  No other part of the args are evaluated.%r[space(6)]If passing arguments to a function, you MUST specify a label to override the name.%r%rLIST_ROSTER houses the dbref#'s of everyone available in the roster. It will display the name/title, and faction of the player. The player can then use +roster/info on the player name OR on the player's numerical order ID to get more information on them.%r%rCommandS:%r+info <target> -- show information on target.%r+roster -- give the roster list of available players.%r+roster/info <number|player name> -- give info on the target.%r[footer()]

&F_FORMAT Roster Object <RO>=[setq(1,sort(iter(graball(v(list_order),*|%0|*|*|*|*|*),[extract(itext(0),3,2,|)]|[extract(itext(0),1,1,|)]|[extract(itext(0),5,1,|)]|[extract(itext(0),6,1,|)]|[extract(itext(0),7,1,|)])))][setq(2,iter(trim(%q1),%ch%c%qz[ifelse(!!$setr(7,extract(itext(0),4,1,|)),[ifelse(match(%q7,NH),,%q7)][setq(4,extract(itext(0),3,1,|))],[setr(7,capstr(lcstr(strip(ifelse(pos(`,setr(4,extract(itext(0),3,1,|))),after(%q4,`),%q4),@))))])][setq(5,%q5 %q4)][ifelse(match(%q7,NH),,%c%qx:%cn )][ifelse(!$setr(f,extract(itext(0),6,1,|)),setq(f,))][setq(j,ifelse(pos(setr(k,extract(itext(0),5,1,|)),-^_),%qk,-))]$:%qf:%qj"|[sub(extract(itext(0),2,1,|),strlen(%q7))]s))][printf(%q2,u(f_format_sub,%1,1),u(f_format_sub,%1,2),u(f_format_sub,%1,3),u(f_format_sub,%1,4),u(f_format_sub,%1,5),u(f_format_sub,%1,6),u(f_format_sub,%1,7),u(f_format_sub,%1,8),u(f_format_sub,%1,9))][setq(5,)]

&F_FORMAT_SUB Roster Object <RO>=[default(%0/[setr(9,extract(%q5,%1,1))],[ifelse(pos(@,%q9),[setq(8,ifelse(!!$after(%q9,:),pedit(after(%q9,:),%%#,%0,%%l,[loc(%0)],%%L,[loc(%0)]),%0))][setq(9,before(%q9,:))][strfunc(lcstr(after(%q9,@)),%q8,:)],%ch%c%qyN/A%cn)])]
&CMD_ROSTER Roster Object <RO>=$+roster:@pemit %#=[setq(z,v(color))][setq(y,v(color_na))][setq(x,v(color_colon))][list(setr(w,listdiff(v(LIST_ROSTER),#-1)),u(f_format_roster,itext(0),inum(0)),,[header(+roster)]%r[printf($:%ch%c%qz-%cn:-6s $:%ch%c%qz-%cn:-40s $:%ch%c%qz-%cn:-30s,%ch%c%qxNum%cn,%ch%c%qxPlayer Name & Title%cn,%ch%c%qxFaction%cn)],%#)][ifelse(!$r(w),[header(+roster)]%r[printf($:%ch%c%qz-%cn:-6s $:%ch%c%qz-%cn:-40s $:%ch%c%qz-%cn:-30s,%ch%c%qxNum%cn,%ch%c%qxPlayer Name & Title%cn,%ch%c%qxFaction%cn)]%r)][footer()]
&LIST_ROSTER Roster Object <RO>=#-1
&F_FORMAT_ROSTER Roster Object <RO>=[printf(%($-4s%) $-40s $-30s,%1,trim([title(%0)] [cname(%0)]),get(%0/faction))]
&CMD_ROSTER_INFO Roster Object <RO>=$+roster/info *:@pemit %#=[setq(z,v(color))][setq(y,v(color_na))][setq(x,v(color_colon))][u(F_INFO[setq(0,ifelse(isnum(%0),extract(listdiff(v(LIST_ROSTER),#-1),%0,1),%0))][!!member(listdiff(v(LIST_ROSTER),#-1),setr(0,pmatch(%q0)))],%q0)]
&CMD_ROSTER_SET Roster Object <RO>=$+roster/set *=*:@break [and(lte(bittype(%#),1),pos(/,%0))]=@pemit %#=[header(+roster)]%r+roster/set: Sorry, you're not a wizard.%r[footer()];@eval [setq(0,ifelse(pos(/,%0),pmatch(before(%0,/)),%#))][setq(1,iter(v(list_order),ifelse(pos(@,itext(0)),,before(itext(0),|))))][setq(2,trim(ifelse(pos(/,%0),after(%0,/),%0)))];@break [!match(%q1,%q2)]=@pemit %#=[header(+roster)]%r+roster/set: Sorry, attribute '%q2' can not be set with this command.%r[footer()];&%q2 %q0=%1;@pemit %#=[header(+roster)]%r+roster/set: '%q2' has been set on [ifelse(match(%q0,%#),yourself,name(%q0))].%r[footer()]
&CMD_ROSTER_SETLIST Roster Object <RO>=$+roster/set:@pemit %#=[header(+roster)]%rFollowing attributes are settable:%r[wrap(trim(iter(v(list_order),ifelse(pos(@,itext(0)),,before(itext(0),|)))),78)]%r[footer()]
&CMD_ROSTER_SET_ALT Roster Object <RO>=$+set *=*:@include/command me/CMD_ROSTER_SET
&CMD_ROSTER_SETLIST_ALT Roster Object <RO>=$+set:@include/command me/CMD_ROSTER_SETLIST
@set Roster Object <RO>=INDESTRUCTABLE
@set Roster Object <RO>=SAFE
@set Roster Object <RO>=INHERIT
